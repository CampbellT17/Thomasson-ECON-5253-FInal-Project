```sas
*Introduction;
*The following code will allow you to pull financial data down from Compustat via SAS Studio with WRDS (sponsored by Wharton), make variables for current and deferred taxes, large positive and large negative book-tax differences, Pre-tax book income, pre-tax cash flows, and pre-tax accruals (all scaled by average total assets), and then run the regressions needed to replicated and extend Hanlon (2005);

*Econ Project Code;

options nocenter ls=max;
option spool; 

*First, we run the replication as instructed in Hanlon (2005) (I go through 2022 just to see how data holds 20+ years later));

*Set up macro loop to allow for running the script for multiple time periods;

%macro run_analysis(start_year, end_year);

*get Compustat data;

data income_&end_year;
    set comp.funda;
    keep gvkey prcc_f cusip tic sich datadate fic fyear ibc fyr at ni pi ibc ajex tlcf csho txc txdfed txdfo ceq txdi txp txt oiadp oancf txpd xidoc;
    where indfmt="INDL" and datafmt='STD' and consol='C' and curcd ="USD" and popsrc='D' and fyear between &start_year and &end_year;
    if not missing(ni+at+pi);
run;

*Bring in more sic code information;
proc sql;
    create table income_&end_year as
        select a.*, b.sic
        from income_&end_year a
        left join comp.namesd as b
        on a.gvkey = b.gvkey;
quit;

*Check for duplicates;

proc sort data=income_&end_year
          out=income_&end_year
          nodupkey; 
          by gvkey fyear; 
run; 

*Load repeatcheck;

%include "/home/ou/campthom17/macros/_repeatcheck.sas";

*Run repeatcheck Macro;
 
%repeatcheck(income_&end_year, gvkey fyear); 

*Create Needed Financial Variables;

data income_&end_year; 
	set income_&end_year;
	mve=prcc_f*csho; *Market Value of Equity;
	if not missing(txdfed+txdfo) then dtax=txdfed+txdfo; *Deferred Taxes;
	else dtax = txdi; *Deferred Taxes;
	ctax=txt-dtax; *Current Taxes;
	if fyear<2018 then dte=dtax/0.35; *Book-Tax Difference;
	else dte=dtax/0.21; *Book-Tax Difference;
	if fyear<2018 then ti=ctax/0.35; *Taxable Income;
	else ti=ctax/0.21; *Taxable Income;
	ptcf=oancf+txpd-xidoc; *Pre-Tax Cash FLows;
	ptacc=pi-ptcf; *Pre-Tax Accruals;
	if sich =. then sich = sic;
run;

*Create Lag Variables;

proc sql;
    create table income_&end_year as
        select a.*, b.at as atm1
        from income_&end_year as a left join income_&end_year as b
        on a.cusip=b.cusip and a.fyear=b.fyear + 1 and a.fyr=b.fyr
        order by cusip, fyear;
quit;

*Create Lead Variables;

proc sql;
    create table income_&end_year as
        select a.*, b.pi as pip1
        from income_&end_year as a left join income_&end_year as b
        on a.cusip=b.cusip and a.fyear=b.fyear + 1 and a.fyr=b.fyr
        order by cusip, fyear;
quit;

*Set filters described by Hanlon (2005);

data income_&end_year;
	set income_&end_year;
	if fyear = &start_year or fyear = &end_year then delete; *Take off years only needed for the lead and lag year boundaries;
	sich2 = int(sich/100);
	if sich2 =. then delete;
	if sich2 = (49+60+61+62+63+64) then delete; *Delete observations from financial services and utilities industries;
	if sich = 9631 then delete; *Delete observations from financial services and utilities industries;
	if mve =. then delete; *Firms with no market value of equity are not useful for our analysis;
	if pi le 0 then delete; *If firms do not have taxable income, they will not apply to this study;
	if ctax le 0 then delete; *If firms do not have current tax expense, they will not apply to this study;
	if tlcf < 0 then delete; *NOL Carryforwards or Carrybacks skew the analysis, so observations with them are removed;
	if tlcf > 0 then delete; *NOL Carryforwards or Carrybacks skew the analysis, so observations with them are removed;
	if at le 0 then delete; *Firms with no assets are not valid observations;
	if atm1 le 0 then delete; *Firms with no assets are not valid observations;
	if missing(pip1+atm1+txpd+oancf+xidoc) then delete; *Require firms with all needed variables;
run;

*Calculate Accrual and Cash Flow Components (Using Instructions from Hanlon (2005));

data income_&end_year;
	set income_&end_year;
	aveta = (at+atm1)/2; *Scalar is average assets;
	scaled_pip1 = pip1/aveta;
	scaled_pi = pi/aveta;
	scaled_ptcf = ptcf/aveta;
	scaled_ptacc = ptacc/aveta;
	scaled_dte = dte/aveta;
	scaled_ti = ti/aveta;
run;

*Load Winsor;
%include "/home/ou/campthom17/macros/_winsor.sas";

*Run Winsor Macro;

options mprint; *Will report in log file the code generated by the macro.;
%winsor(dsetin=income_&end_year, dsetout=incomew_&end_year, byvar=none, vars=scaled_pip1 scaled_pi scaled_ptcf scaled_ptacc scaled_dte, type=winsor, pctl=1 99);

proc univariate data=incomew_&end_year;
    var scaled_dte;
	histogram scaled_dte / midpoints=(-0.125 to 0.125 by 0.025)
                    	   endpoints=(-0.125 to 0.125 by 0.025)
                    	   vscale=percent outhistogram=hist_&end_year
    					   odstitle="Distribution of Book-Tax Differences";
run;

*Sort Book-Tax Differences into Quintiles;

proc rank data=incomew_&end_year
   out=incomew_ranked_&end_year
   groups=5;
   var scaled_dte;
   ranks btd_quintile;
run;

*Create Dummy Variables and Related Interaction Terms;

data incomew_ranked_&end_year;
	set incomew_ranked_&end_year;
	if btd_quintile = 4 then lpbtd = 1;
	else lpbtd = 0;
	if btd_quintile = 0 then lnbtd = 1;
	else lnbtd = 0;
	if btd_quintile = 1 or btd_quintile = 2 or btd_quintile = 3 then small_btd = 1;
	else small_btd = 0;
	if btd_quintile = 0 or btd_quintile = 4 then lbtd = 1;
	else lbtd = 0;
	pi_lnbtd = scaled_pi * lnbtd;
	pi_lpbtd = scaled_pi * lpbtd;
	pi_lbtd = scaled_pi * lbtd;
	ptcf_lnbtd = scaled_ptcf * lnbtd;
	ptcf_lpbtd = scaled_ptcf * lpbtd;
	ptcf_lbtd = scaled_ptcf * lbtd;
	ptacc_lnbtd = scaled_ptacc * lnbtd;
	ptacc_lpbtd = scaled_ptacc * lpbtd;
	ptacc_lbtd = scaled_ptacc * lbtd;
run;

Title "Persistence of Pre-Tax Earnings";
proc reg data=incomew_ranked_&end_year outest=model1_&end_year;
    model scaled_pip1 = scaled_pi;
run;

Title "Persistence of Pre-Tax Earnings Controlled for Size of Book-Tax Differences";
proc reg data=incomew_ranked_&end_year outest=model2_&end_year;
    model scaled_pip1= lbtd scaled_pi pi_lbtd;
run;

Title "Persistence of Pre-Tax Earnings Controlled for Size and Sign of Book-Tax Differences";
proc reg data=incomew_ranked_&end_year outest=model3_&end_year;
    model scaled_pip1= lnbtd lpbtd scaled_pi pi_lnbtd pi_lpbtd;
run;

Title "Persistence of Pre-Tax Earnings by Earnings Component";
proc reg data=incomew_ranked_&end_year outest=model4_&end_year;
    model scaled_pip1 = scaled_ptcf scaled_ptacc;
run;

Title "Persistence of Pre-Tax Earnings by Earnings Component Controlled for Size of Book-Tax Differences";
proc reg data=incomew_ranked_&end_year outest=model5_&end_year;
    model scaled_pip1 = lbtd scaled_ptcf ptcf_lbtd scaled_ptacc ptacc_lbtd;
run;

Title "Persistence of Pre-Tax Earnings by Earnings Component Controlled for Size and Sign of Book-Tax Differences";
proc reg data=incomew_ranked_&end_year outest=model6_&end_year;
    model scaled_pip1 = lnbtd lpbtd scaled_ptcf ptcf_lnbtd ptcf_lpbtd scaled_ptacc ptacc_lnbtd ptacc_lpbtd;
run;

proc means data=incomew_ranked_&end_year n mean std q1 q3 min max;
    var scaled_pip1 scaled_pi scaled_ptcf scaled_ptacc scaled_dte aveta lnbtd lpbtd small_btd lbtd pi_lnbtd pi_lpbtd pi_lbtd ptcf_lnbtd ptcf_lpbtd ptcf_lbtd ptacc_lnbtd ptacc_lpbtd ptacc_lbtd;
run;

*Set up the two time periods for the macro to run the script for;

%mend;

%run_analysis(1993, 2001);
%run_analysis(1993, 2023);
```
